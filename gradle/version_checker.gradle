// This module adds a task that will update the latest version in Jared's
// update checker API. This is a private service which requires an API key to
// use. Contact https://twitter.com/jaredlll08 for more info.
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

apply from: 'gradle/property_helper.gradle'

task updateVersionTracker {
    
    if (!project.hasProperty('versionTrackerAPI') || !project.hasProperty('versionTrackerUsername')) {
    
        project.logger.warn('The updateVersionTracker task was ran but authentication was not provided!')
    }
    
    onlyIf {
    
        project.hasProperty('versionTrackerAPI') && project.hasProperty('versionTrackerUsername')
    }
    
    doLast {

        def username = getRequiredString('versionTrackerUsername')
        def apiKey = getRequiredString('versionTrackerKey')
        
        // Creates a Map that acts as the Json body of the API request.
        def body = [
            'author': username,
            'projectName': project.ext.modId,
            'gameVersion': project.ext.mcVersion,
            'projectVersion': project.version,
            'homepage': project.ext.modDownload,
            'uid': apiKey
        ]
    
        project.logger.lifecycle("Version Check: ${project.ext.modId} for ")
        // Opens a connection to the version tracker API and writes the payload JSON.
        def req = new URL(project.findProperty('versionTrackerAPI')).openConnection()
        req.setRequestMethod('POST')
        req.setRequestProperty('Content-Type', 'application/json; charset=UTF-8')
        req.setRequestProperty('User-Agent', "${project.ext.modName} Tracker Gradle")
        req.setDoOutput(true)
        req.getOutputStream().write(JsonOutput.toJson(body).getBytes("UTF-8"))

        // For the request to be sent we need to read data from the stream.
        project.logger.lifecycle("Version Check: Status ${req.getResponseCode()}")
        project.logger.lifecycle("Version Check: Response ${req.getInputStream().getText()}")
    }
}